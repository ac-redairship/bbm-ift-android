apply plugin: 'com.android.library'
apply plugin: 'checkstyle'

task checkstyle(type: Checkstyle) {
  configFile rootProject.file('checkstyle.xml')
  source 'src/main/java'
  ignoreFailures false
  showViolations true
  include '**/*.java'

  classpath = files()
}

afterEvaluate {
  if (project.tasks.findByName('check')) {
    check.dependsOn('checkstyle')
  }
}

android {
  compileSdkVersion 31

  defaultConfig {
    minSdkVersion 14
  }

  lintOptions {
    warning 'MissingTranslation'
    textReport true
    textOutput 'stdout'
  }

  testOptions {
    unitTests {
      includeAndroidResources = true
    }
    unitTests.all {
      testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        exceptionFormat "full"
      }
    }
  }
}


def deps = [
        // Test dependencies
        androidXCore       : 'androidx.core:core:1.9.0-alpha01',
        junit              : 'junit:junit:4.13.2',
        robolectric        : 'org.robolectric:robolectric:4.7.3',
        intellijannotations: 'org.jetbrains:annotations:16.0.2',
        truth              : 'com.google.truth:truth:1.0'
]

dependencies {
  implementation deps.androidXCore
  testImplementation deps.junit
  testImplementation deps.robolectric
  testImplementation deps.intellijannotations
  testImplementation deps.truth
}

allprojects {
  plugins.withId("com.vanniktech.maven.publish") {
    mavenPublish {
      sonatypeHost = "S01"
    }
  }
}
